options {
  MULTI=true;
  VISITOR=true;
  NODE_DEFAULT_VOID=true;
  STATIC=false;
}

PARSER_BEGIN(Copy)

package com.google.cloud.spanner.pgadapter.parsers.copy;

import java.nio.ByteBuffer;
import java.nio.charset.StandardCharsets;

public class Copy {

  public static void parse(String stmt, CopyTreeParser.CopyOptions options) throws Exception {
    java.io.InputStream stmtStream = new java.io.ByteArrayInputStream(stmt.getBytes());
    Copy tree = new Copy(stmtStream);
    try {
      ASTStart node = tree.Start();
      CopyVisitor v = new CopyTreeParser(options);
      node.jjtAccept(v, null);
    } catch (Exception e) {
      System.err.println(e.toString());
      throw e;
    }
  }

}

PARSER_END(Copy)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

TOKEN [IGNORE_CASE] : /* KEYWORDS */
{
  <K_STDIN: "STDIN">
| <K_STDOUT: "STDOUT">
| <K_TO: "TO">
| <K_FROM: "FROM">
| <K_COPY: "COPY">
| <K_WITH: "WITH">
}

TOKEN [IGNORE_CASE] : /* COPY OPTIONS */
{
  <O_BINARY: "BINARY">
| <O_CSV: "CSV">
| <O_TEXT: "TEXT">
| <O_FORMAT: "FORMAT">
| <O_FREEZE: "FREEZE">
| <O_DELIMITER: "DELIMITER">
| <O_NULL: "NULL">
| <O_HEADER: "HEADER">
| <O_QUOTE: "QUOTE">
| <O_ESCAPE: "ESCAPE">
| <O_FORCE_QUOTE: "FORCE_QUOTE">
| <O_FORCE_NOT_NULL: "FORCE_NOT_NULL">
| <O_FORCE_NULL: "FORCE_NULL">
| <O_ENCODING: "ENCODING">
| <O_TRUE: "TRUE">
| <O_FALSE: "FALSE">
}

TOKEN : /* IDENTIFIERS */
{
  < QUOTED_IDENTIFIER: ("\"" <IDENTIFIER> "\"" | "\'" <IDENTIFIER> "\'") >
|
  < IDENTIFIER: ("'" <ID> "'" | <ID>) >
|
  < QUOTED_CHAR: ("\"" <SINGLE_CHAR> "\"" | "\'" <SINGLE_CHAR> "\'") >
|
  < #SINGLE_CHAR: (["!"-"'","*"-"/",":","<"-"@","["-"`"]) >
|
  < #ID: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #DIGIT: ["0"-"9"] >
|
  < #LETTER: ["_","a"-"z","A"-"Z"] >
|
  < #LOWER_CASE_LETTER: ["_","a"-"z"] >
}

/** Main production. */
ASTStart Start() #Start : {}
{
  Expression() ";"
  { return jjtThis; }
}

/** An Expression. */
void Expression() : {}
{
  CopyStmt()
}

/** Copy Statement. */
void CopyStmt() #CopyStatement : {}
{
  <K_COPY> qualifiedName() optColumnList() copyFrom() copyFileName() optWith() copyOptions()
}

/** Qualified Name. Zero or more namespaces qualifiers can prefix the name. */
void qualifiedName() #QualifiedName : {}
{
  namespace()
}

void namespace() #Namespace : {}
{
    LOOKAHEAD(2)
    identifier() "." namespace()
  | LOOKAHEAD(2)
    quotedIdentifier() "." namespace()
  | identifier()
  | quotedIdentifier()
}

/** Optional Column List */
void optColumnList() : {}
{
  LOOKAHEAD(2)
  "(" columnList() ")"
  | {} /* EMPTY */
}

/** Column List. */
void columnList() #ColumnList : {}
{
  LOOKAHEAD(2)
  columnElem() "," columnList()
  | columnElem()
}

/** Column Element. */
void columnElem() #ColumnElement : {}
{
  identifier()
  | quotedIdentifier()
}

/** Copy From */
void copyFrom() #CopyDirection : {}
{
  <K_FROM> { jjtThis.setDirection("FROM"); }
  | <K_TO>	{
    jjtThis.setDirection("TO");
    throw new ParseException("COPY TO is not supported.");
  }
}

void copyFileName() #Filename :
{
  Object o;
}
{
  <K_STDIN> {jjtThis.setName("STDIN");}
  | <K_STDOUT> {jjtThis.setName("STDOUT");}
  | identifier()
}

/** Optional With */
void optWith() : {}
{
  <K_WITH>
  | {} /* EMPTY */
}

/** Optional list of copy options */
void copyOptions() #CopyOptions : {}
{
  LOOKAHEAD(2)
  "(" copyOptionList() ")"
  | copyOptionList()
}

void copyOptionList() #CopyOptionList : {}
{
  copyOptionElement() copyOptionList()
  | {} /* EMPTY */
}

void copyOptionElement() #CopyOptionElement : {}
{
    <O_FORMAT> formatType() {
      jjtThis.setName("FORMAT");
    }
  | formatType() {
      jjtThis.setName("FORMAT");
  }
  | <O_FREEZE> {
      jjtThis.setName("FREEZE");
      throw new ParseException("FREEZE is not supported.");
    }
  | <O_DELIMITER> singleChar() {
      jjtThis.setName("DELIMITER");
    }
  | <O_NULL> quotedIdentifier() {
      jjtThis.setName("NULL");
    }
  | <O_HEADER> booleanOption() {
      jjtThis.setName("HEADER");
    }
  | <O_QUOTE> singleChar() {
      jjtThis.setName("QUOTE");
    }
  | <O_ESCAPE> singleChar() {
      jjtThis.setName("ESCAPE");
    }
  | LOOKAHEAD(3)
    <O_FORCE_QUOTE> "(" columnList() ")" {
      jjtThis.setName("FORCE_QUOTE");
      throw new ParseException("FORCE_QUOTE is not supported.");
    }
  | LOOKAHEAD(3)
    <O_FORCE_NOT_NULL> "(" columnList() ")" {
      jjtThis.setName("FORCE_NOT_NULL");
      throw new ParseException("FORCE_NOT_NULL is not supported.");
    }
  | LOOKAHEAD(3)
    <O_FORCE_NULL> "(" columnList() ")" {
      jjtThis.setName("FORCE_NULL");
      throw new ParseException("FORCE_NULL is not supported.");
    }
  | <O_ENCODING> identifier() {
      jjtThis.setName("ENCODING");
      throw new ParseException("ENCODING is not supported.");
    }
}

void formatType() #FormatType : {}
{
    <O_TEXT> {
      jjtThis.setFormat("TEXT");
    }
  | <O_BINARY> {
      jjtThis.setFormat("BINARY");
    }
  | <O_CSV> {
      jjtThis.setFormat("CSV");
    }
}

/** An Identifier. */
void identifier() #ID :
{
  Token t;
}
{
  t=<IDENTIFIER>
  {
    // Only support US_ASCII for now. UTF-8 support will be added later.
    ByteBuffer bytes = StandardCharsets.US_ASCII.encode(t.image.toLowerCase());
    jjtThis.setName(new String(bytes.array(), StandardCharsets.US_ASCII));
  }
}

void quotedIdentifier() #ID :
{
  Token t;
}
{
  t=<QUOTED_IDENTIFIER>
  {
    // Only support US_ASCII for now. UTF-8 support will be added later.
    ByteBuffer bytes = StandardCharsets.US_ASCII.encode(t.image.substring(1, t.image.length()-1));
    jjtThis.setName(new String(bytes.array(), StandardCharsets.US_ASCII));
  }
}

void singleChar() #SingleChar :
{
  Token t;
}
{
  t=<QUOTED_CHAR>
  {
    jjtThis.setChar(t.image.charAt(1));
  }
}

void booleanOption() #Boolean : {}
{
    <O_TRUE> { jjtThis.setBool(true); }
  | <O_FALSE> { jjtThis.setBool(false); }
}
